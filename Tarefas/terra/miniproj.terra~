#include "/home/terra/TerraNet_v0.1/terra/TerraNet.defs"

var ushort nodeId = getNodeId();
var ushort fatherNode = 0;
var ushort nMsgs=0;

pktype t_tMsg from radioMsg with
	var ubyte[4] d8;
	var ushort[4] d16;
	var ulong[2] d32;
end

var t_tMsg ownMsg;
var ubyte stat;

/* routing initialization */
if nodeId==11 then //sends first broadcast
	fatherNode=1;
else //awaits broadcast then sends its own
	ownMsg=await RECEIVE();
	fatherNode=ownMsg.source;
end
ownMsg.source=nodeId;
ownMsg.target=BROADCAST;
emit SEND(ownMsg);
await SEND_DONE;

/* execution loops */
par do
	//loop do //generates its own messages and put on its queue
		await 4s;
		ownMsg.source=nodeId;
		ownMsg.target=fatherNode;
		
		emit REQ_TEMP();
		ownMsg.d16[0] = await TEMP;
		
		stat=qPut(ownMsg);
	//end
with //await others' msgs and put on its queue
	var t_tMsg receivedMsg;
	loop do
		receivedMsg= await RECEIVE();
		
		//only queue it if its not a routing initialization msg
		if receivedMsg.target!=BROADCAST then 
			receivedMsg.target=fatherNode;
			stat=qPut(receivedMsg);
		end
	end
with //manage msg queue
	var t_tMsg queueMsg;
	loop do
		par/and do//activate atmost every 1s
			par/and do//activate upon Qready AND its id based seed
				await Q_READY;
			with
				await (6*(2*(nodeId%10)+(nodeId/10)))ms;
			end
			
			loop do //emptying queue loop
				stat=qGet(queueMsg);

				inc nMsgs;
				if nodeId == 11 then
					queueMsg.d16[1]=nMsgs;
				end
				
				emit SEND(queueMsg);
				await SEND_DONE;
			
				if qSize()==0 then
					break;
				end
			end //end queue loop
		with
			await 1s;
		end//par/and
	end//loop
end



